#compdef distrobox-enter

source /usr/share/zsh/site-functions/_distrobox_shared_functions

_distrobox-enter() {
    local -a options
    local expl
    local state

    options=(
      '(--name -n)'{-n,--name}'[name for the distrobox]:container:_distrobox_containers'
      '--[end arguments and execute the rest as command to execute at login]:command:_command_names'
      '(--no-tty -T)'{-T,--no-tty}'[do not instantiate a tty]'
      '(--no-workdir -nw)'{-nw,--no-workdir}'[always start the container from container home directory]'
      '(--additional-flags -a)'{-a,--additional-flags}'[additional flags to pass to the container manager command]:flags:'
      '(--help -h)'{-h,--help}'[show this message]'
      '(--root -r)'{-r,--root}'[launch podman/docker/lilipod with root privileges]'
      '(--dry-run -d)'{-d,--dry-run}'[only print the container manager command generated]'
      '(--verbose -v)'{-v,--verbose}'[show more verbosity]'
      '(--version -V)'{-V,--version}'[show version]'
    )
    _arguments -C \
      '1: :->container' \
      $options[@]

    case $state in
      container)
        if (( CURRENT == 2 )); then
          _distrobox_containers && return 0
        fi
        ;;
      *)
        _arguments $options[@]
        ;;
    esac
}

_distrobox-enter
